#!/usr/bin/perl
###############################################################################
##
# FILE:		reldate
# PRODUCT:	tools
# AUTHOR:	Ingo Karkat <ingo@karkat.de>
# DATE CREATED:	09-Feb-2016
#
###############################################################################
# CONTENTS:
#
# REMARKS:
#
# @(#)reldate	$Id$ reldate
###############################################################################

package Local::Reldate;

use List::Util qw(first);
use Modern::Perl;
use Getopt::Long qw(GetOptionsFromArray);
use Pod::Usage;
use Time::Duration;
use Time::Local;
use Time::Piece;

my @monthsShort = qw/Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec/;
my @monthsLong = qw/January February March April May June July August September October November December/;
my $monthsExpr = join('|', @monthsShort, @monthsLong);
my $nowFuncref = sub { return $^T; };
my $now = $nowFuncref->();
my $baseEpoch;
my @defaultMassages = (\&massageWeekdays, \&massageFull);
my ($datePattern, $keepWidth, $precision, $isPreferPast, $isPreferFuture, $isWithWeekday, $prefix, $suffix, $isRelative, $isDelta, $isEach);

sub setNowSource {
    (undef, $nowFuncref) = @_;
    $now = $nowFuncref->();
}

sub numericalMonth {
    my $month = shift;
    return $month - 1 if $month =~ m/^\d+$/;

    my $index = first { $monthsShort[$_] eq $month } 0 .. $#monthsShort;
    return $index if defined $index;

    $index = first { $monthsLong[$_] eq $month } 0 .. $#monthsLong;
    return $index if defined $index;

    die "Should have parsed month";
}

sub toEpoch {
    my ($seconds, $minutes, $hours, $day, $month, $year, $timezone) = @_;
    my $timeConverter = ($timezone ? \&timegm : \&timelocal);

    my $epoch = $timeConverter->($seconds, $minutes, (defined($hours) ? $hours : 12), $day, numericalMonth($month), (defined($year) ? $year : Time::Piece->strptime($now, '%s')->year));

    if (! defined($year) && $isPreferPast && $now - $epoch < 0) {
	$epoch = $timeConverter->($seconds, $minutes, (defined($hours) ? $hours : 12), $day, numericalMonth($month), (defined($year) ? $year : Time::Piece->strptime($now, '%s')->year - 1));
    } elsif (! defined($year) && $isPreferFuture && $now - $epoch > 0) {
	$epoch = $timeConverter->($seconds, $minutes, (defined($hours) ? $hours : 12), $day, numericalMonth($month), (defined($year) ? $year : Time::Piece->strptime($now, '%s')->year + 1));
    }

    return $epoch;
}

sub massageWeekdays {
    my $relativeDate = shift;
    return $relativeDate unless ($isWithWeekday);
    return $relativeDate unless ($relativeDate =~ /^(\d+) days? (ago|from now)$/);

    my $isPast = ($2 eq "ago");
    my $isAdjacentWeekDay = ($1 <= 7);

    my $weekDay = ((localtime($now))[6] + 7 + ($isPast ? -1 : 1) * $1) % 7;
    my $weekDayName = qw(
	Sunday
	Monday
	Tuesday
	Wednesday
	Thursday
	Friday
	Saturday
    )[$weekDay];
    return ($isAdjacentWeekDay ?
	($isPast ? "the previous " : "coming ") . $weekDayName :
	"$relativeDate (a $weekDayName)"
    );
}
sub massageFull {
    my $relativeDate = shift;

    # Massage the output from Time::Duration to make it more amenable to my
    # preferences.
    $relativeDate = 'just now' if ($relativeDate eq 'right now');
    $relativeDate = 'yesterday' if ($relativeDate eq '1 day ago');
    $relativeDate = 'tomorrow' if ($relativeDate eq '1 day from now');
    $relativeDate =~ s/(.*) from now$/in $1/;

    return $relativeDate;
}

sub massageForShortTimespan {
    my $relativeDate = shift;

    $relativeDate = 'just now' if ($relativeDate eq 'right now');
    $relativeDate =~ s/ from now$| ago$//;

    return $relativeDate;
}

sub massageToFirst {
    my $relativeDate = shift;

    $relativeDate = 'at the same time' if ($relativeDate eq 'right now');
    $relativeDate =~ s/(.*) ago$/$1 before it/;
    $relativeDate =~ s/(.*) from now$/$1 later than it/;

    return $relativeDate;
}

sub massageToFirstAgain {
    return 'also ' . massageToFirst(@_);
}

sub massageEach {
    my $relativeDate = shift;

    $relativeDate = 'at that time' if ($relativeDate eq 'right now');
    $relativeDate =~ s/(.*) ago$/$1 before that/;
    $relativeDate =~ s/(.*) from now$/$1 later than that/;

    return $relativeDate;
}

sub reldateNormal {
    my $string = shift;
    $string =~ 	s/${datePattern}/$prefix . reldate(\@defaultMassages, $now, toEpoch($+{seconds}, $+{minutes}, $+{hours}, $+{day}, $+{month}, $+{year}, $+{timezone}), $&) . $suffix/ge;
    return $string;
}

sub reldateBase {
    my $string = shift;
    $string =~ s/${datePattern}/deltaReplacement(1, toEpoch($+{seconds}, $+{minutes}, $+{hours}, $+{day}, $+{month}, $+{year}, $+{timezone}), $&)/ge;
    return $string;
}

sub reldateDelta {
    my $string = shift;
    my $count = 0;
    $string =~ s/${datePattern}/deltaReplacement($count++, toEpoch($+{seconds}, $+{minutes}, $+{hours}, $+{day}, $+{month}, $+{year}, $+{timezone}), $&)/ge;
    return $string;
}

sub timespanToReldate {
    my $massagesRef = shift;
    my $timespan = shift;
    my $reldate = ago($timespan, $precision);

    foreach my $massage (@$massagesRef) {
	$reldate = $massage->($reldate);
    }
    return $reldate;
}

sub reldate {
    my ($massagesRef, $referenceEpoch, $parsedEpoch, $originalDate) = @_;
    my $relativeDate = timespanToReldate($massagesRef, $referenceEpoch - $parsedEpoch);

    if ($keepWidth ne 'no') {
	my $originalWidth = length($originalDate);
	my $availableWidth = $originalWidth - length($prefix) - length($suffix); $availableWidth = 0 if $availableWidth < 0;
	my $flag = ($keepWidth eq 'right-aligned' ? '' : '-');
	$relativeDate = sprintf("%${flag}${availableWidth}s", $relativeDate);
    }
    return $relativeDate;
}

sub deltaReplacement {
    my ($count, $epoch, $originalDate) = @_;

    my @massages = ($isEach ?
	\&massageEach :
	($count == 1) ?
	    \&massageToFirst :
	    \&massageToFirstAgain
    );
    my $isFirst = ($count == 0);
    my $isRecordBase = ($isEach ? 1 : $isFirst);

    my $replacement;
    if ($isRelative) {
	$replacement = ($isFirst ?
	    $originalDate :
	    $prefix . reldate(\@massages, $baseEpoch, $epoch, $originalDate) . $suffix
	);
    } else {
	$replacement = $prefix . ($isFirst ?
	    reldate(\@defaultMassages, $now, $epoch, $originalDate) :
	    reldate(\@massages, $baseEpoch, $epoch, $originalDate)
	) . $suffix;
    }

    if ($isRecordBase) {
	$baseEpoch = $epoch;
    }

    return $replacement;
}

my ($isDoubleDash, $timespan, $epoch, $within, $reset, $hasDifferentBase);

sub process {
    my ($argumentsRef, $inputHandleRef) = @_;

    if (@$argumentsRef == 1 && ! $isDoubleDash) {
	my $file = @$argumentsRef[0];
	unless (-e $file) {
	    warn "$file does not exist!\n";
	    exit 1;
	}
	my $age = $now - (stat($file))[9];  # 9 = modtime
	print $prefix, timespanToReldate(\@defaultMassages, $age), $suffix, "\n";
    } elsif (defined $timespan) {
	my @massages = ($timespan =~ /^[+-]/ ? @defaultMassages : (\&massageWeekdays, \&massageForShortTimespan));
	print $prefix, timespanToReldate(\@massages, $timespan), $suffix, "\n";
    } elsif (defined $epoch) {
	print $prefix, timespanToReldate(\@defaultMassages, $now - $epoch), $suffix, "\n";
    } else {
	my $splitPattern = qr/^.*$/s;
	if (defined $reset) {
	    $/ = $reset;
	} elsif (($within // '') eq 'all') {
	    $/ = undef;
	}
	if (($within // '') eq 'line') {
	    $splitPattern = qr/^.*$/m;
	} elsif (defined $within and $within ne 'all' and $within ne 'line') {
	    $splitPattern = $within;
	}

	my $algorithm;
	if ($hasDifferentBase) {
	    $baseEpoch = $now;
	    $algorithm = \&reldateBase;
	} elsif ($isRelative or $isDelta) {
	    $algorithm = \&reldateDelta;
	} else {
	    $algorithm = \&reldateNormal;
	}

	while (readline($inputHandleRef)) {
	    # Update the current time for each record, to accommodate long-running
	    # parsing.
	    $now = $nowFuncref->() unless $hasDifferentBase;

	    s/${splitPattern}/$algorithm->($&)/ges;
	    print;
	}
    }
}


sub run {
    my (undef, $argumentsRef, $inputHandleRef) = @_;

    $datePattern = join('|', (
	    qr/\b(?<year>\d\d\d\d)(?<month>[01]\d)(?<day>[0123]\d)(?:[ _-](?<hours>[012]\d)(?<minutes>[0-5]\d)(?<seconds>[0-5]\d)?)?\b/,
	    qr/\b(?<year>\d\d\d\d)-(?<month>(?:[01]\d|${monthsExpr}))-(?<day>[0123]\d)(?: (?<hours>[012]\d):(?<minutes>[0-5]\d)(?::(?<seconds>[0-5]\d))?)?\b/,
	    qr/\b(?<day>[0123]\d)-(?<month>(?:[01]\d|${monthsExpr}))-(?<year>\d\d\d\d)(?: (?<hours>[012]\d):(?<minutes>[0-5]\d)(?::(?<seconds>[0-5]\d))?)?\b/,
	    qr/\b(?<month>${monthsExpr})\s+(?<day>[0123]?\d),?(?:\s+(?<year>\d\d\d\d))?(?: (?<hours>[012]\d):(?<minutes>[0-5]\d)(?::(?<seconds>[0-5]\d))?)?\b/,
	    qr/\b(?<year>\d\d\d\d)-(?<month>[01]\d)-(?<day>[0123]\d)T(?<hours>[012]\d):(?<minutes>[0-5]\d):(?<seconds>[0-5]\d)(?<timezone>Z)\b/)
    );
    $keepWidth = 'no';
    $precision = 1;
    $isPreferPast = 0;
    $isPreferFuture = 0;
    $isWithWeekday = 0;
    $prefix = '';
    $suffix = '';
    $isRelative = 0;
    $isDelta = 0;
    $isEach = 0;

    $isDoubleDash = 0;
    $timespan = undef;
    $epoch = undef;
    $within = undef;
    $reset = undef;
    $hasDifferentBase = 0;

    my $help = 0;
    my $man = 0;

    $isDoubleDash = grep(/^--$/, @$argumentsRef);	# Need to check this before Getopt::Long swallows the -- separator.

    GetOptionsFromArray($argumentsRef,
	'help|h|?' => \$help,
	'man' => \$man,
	'date-pattern=s' => \$datePattern,
	'relative-to=s' => sub {
	    my $baseDate = $_[1];
	    unless (($now = $baseDate) =~ s/^${datePattern}$/toEpoch($+{seconds}, $+{minutes}, $+{hours}, $+{day}, $+{month}, $+{year}, $+{timezone})/e ) {
		warn "$baseDate is not a valid BASE-DATE!\n";
		exit 2;
	    }
	    $hasDifferentBase = 1;
	},
	'relative-to-epoch=i' => sub {
	    $now = $_[1];
	    $hasDifferentBase = 1;
	},
	'relative-to-first' => sub {
	    $isRelative = 1;
	    $isEach = 0;
	},
	'relative-each' =>  sub {
	    $isRelative = 1;
	    $isEach = 1;
	},
	'delta-to-first' => sub {
	    $isDelta = 1;
	    $isEach = 0;
	},
	'delta-each' => sub {
	    $isDelta = 1;
	    $isEach = 1;
	},
	'within=s' => \$within,
	'reset=s' => \$reset,
	'keep-width:s' => \$keepWidth,
	'precision=i' => \$precision,
	'prefer-past' => \$isPreferPast,
	'prefer-future' => \$isPreferFuture,
	'with-weekday' => \$isWithWeekday,
	'prefix=s' => \$prefix,
	'suffix=s' => \$suffix,
	'timespan=s' => \$timespan,
	'epoch=i' => \$epoch,
    ) or pod2usage(2);
    pod2usage(-exitval => 0, -verbose => 0) if $help;
    pod2usage(-exitval => 0, -verbose => 2) if $man;
    pod2usage(-exitval => 2, -message => "--prefer-past and --prefer-future are mutually exclusive") if ($isPreferPast && $isPreferFuture);

    process($argumentsRef, $inputHandleRef);
}

__PACKAGE__->run(\@ARGV, \*ARGV) unless caller();

__END__

=head1 NAME

reldate - Show relative dates instead of absolute ones

=head1 SYNOPSIS

 echo TEXT-WITH-DATES | reldate [--date-pattern PATTERN] [--relative-to BASE-DATE|--relative-to-epoch BASE-EPOCH|--relative-to-first|--relative-each|--delta-to-first|--delta-each] [--within (line|PATTERN|all] [--reset RESET-PATTERN)] [--keep-width] [--precision N] [--prefer-past|--prefer-future] [--with-weekday] [--prefix STRING] [--suffix STRING]
 reldate [...] -- FILE, reldate [...] FILE1 FILE2 ...

 reldate [...] --timespan [+-]SECONDS
 reldate [...] --epoch EPOCH
 reldate [...] FILE

    Options:
    --date-pattern PATTERN	parse PATTERN as a date
    --relative-to BASE-DATE     make all dates relative to BASE-DATE
    --relative-to-epoch EPOCH   make all dates relative to EPOCH
    --relative-to-first         take the first date as an absolute base, then make all subsequent ones relative to the first
    --relative-each             take the first date as an absolute base, then make all subsequent ones relative to the previous one
    --delta-to-first            with the first date as (also relative) base, make all subsequent ones relative to that
    --delta-each                make all subsequent dates relative to the previous one
    --within (line|PATTERN|all) ... first date each line / in text matching PATTERN (within a line / the block delimited by RESET-PATTERN) / entire input
    --reset RESET-PATTERN       ... until RESET-PATTERN matches again
    --keep-width		maintain width of replaced date
    --precision N		max number of time units used
    --prefer-past		assume incomplete date is in past
    --prefer-future		assume incomplete date is in future
    --with-weekday		use "coming/the previous Monday" instead of "in two days /ago"
    --prefix STRING		text before relative date
    --suffix STRING		text after relative date

    --timespan [+-]SECONDS      convert SECONDS difference to relative date
    --epoch EPOCH               convert EPOCH seconds to relative date

    -?|-h|--help		brief help message
    --man			full documentation

=head1 OPTIONS

=over 8

=item B<--date-pattern>

Parse any match of PATTERN as a date.

By default, parses the following date formats:

 %Y%m%d
 %Y%m%d[ _-]%H:%M
 %Y%m%d[ _-]%H:%M:%S
 %Y-%m-%d
 %Y-%m-%d %H:%M
 %Y-%m-%d %H:%M:%S
 %d-%[mbB]-%Y
 %d-%[mbB]-%Y %H:%M
 %d-%[mbB]-%Y %H:%M:%S
 %[mbB] %d[,] %Y
 %[mbB] %d[,] %Y %H:%M
 %[mbB] %d[,] %Y %H:%M:%S
 %[mbB] %d[,] %H:%M
 %[mbB] %d[,] %H:%M:%S
 %Y-%m-%dT%H:%M:%SZ

Example PATTERNs:

 \b(?<year>\d\d\d\d)-(?<month>\d\d)-(?<day>\d\d)T(?<hours>\d\d):(?<minutes>\d\d):(?<seconds>\d\d)(?<timezone>Z)\b
 \b(?<year>\d\d\d\d)(?<month>\d\d)(?<day>\d\d)\b

=item B<--relative-to>, B<--relative-to-epoch>

Relate dates to passed BASE-DATE (also parsed according to C<--date-pattern>) or BASE-EPOCH instead of comparing to the current date. Relative dates will refer to I<it> and I<at the same time> instead of now (except when used together with C<--delta-to-first>, where I<it> will refer to the first date).

Also works for C<--epoch> and C<FILE>.

=item B<--relative-to-first>

Within the parsed block (cp. C<--within> and C<--reset>), the first found date is printed as-is, and taken as the base for the rest of the block. All subsequent dates refer to that first one with the subject I<it>.

=item B<--relative-each>

Within the parsed block (cp. C<--within> and C<--reset>), the first found date is printed as-is. Each following date is made relative to the preceding one, which is referred to as I<that>.

=item B<--delta-to-first>

Within the parsed block (cp. C<--within> and C<--reset>), the first found date is taken as the base for the rest of the block. All subsequent dates refer to that first one with the subject I<it>.

=item B<--delta-each>

Within the parsed block (cp. C<--within> and C<--reset>), each date is made relative to the preceding one, which is referred to as I<that>.

=item B<--within>

Specifies what I<first> represents for C<--relative-to-first>, C<--relative-each> (kept absolute), C<--delta-to-first>, C<--delta-each> (make relative to the base date). For the normal processing, PATTERN can limit where translation of absolute to relative dates happens.

The default scope is individual lines, or ranges delimited by RESET-PATTERN. In the latter case, C<line> explicitly switches back to first in each line. C<all> reads the entire input as one big record, so the first date really only is the very first one.

=item B<--reset>

Separator pattern (unanchored) that makes up ranges where C<--within> is then applied. By default, individual lines are processed.

=item B<--keep-width>

Pad any shorter relative date so that it occupies the same space as the original one.

=item B<--precision>

No more than N units will be used in expressing the time duration. Default is 1, so it uses only one of seconds / minutes / hours / days / ...

=item B<--prefer-past>

If an incomplete date specification (e.g. without a year) is parsed, assume that the date is in the past.

=item B<--prefer-future>

If an incomplete date specification (e.g. without a year) is parsed, assume that the date is in the future.

=item B<--with-weekday>

For days that are within 7 days, use "coming/the previous Monday" instead of "in two days /ago". For days that are longer in the future or past, append the weekday to the relative date.

=item B<--prefix>

STRING to put before a recognized relative date. E.g. [ or ^[[01m.

=item B<--suffix>

STRING to put after a recognized relative date. E.g. ] or ^[[0m.

=item B<--timespan>

SECONDS of time difference to directly convert to a relative date. Negative SECONDS lie in the future.

Without a [+-] sign, prints a neutral representation (e.g. I<16 minutes> instead of I<16 minutes ago>).

=item B<--epoch>

Convert EPOCH seconds since the epoch (1970-01-01 UTC) to relative date.

=item B<--help>, B<-h>, B<-?>

Print a brief help message and exits.

=item B<--man>

Prints the manual page and exits.

=back

=head1 DESCRIPTION

B<reldate> turns absolute dates (e.g. I<09-02-2016>) into timespans relative to the current date (I<2 days ago>).

It parses text from stdin (or passed FILE(s)) and replaces any date [as specified by PATTERN] with a relative one.

It directly converts the passed timespan (in SECONDS), or the passed EPOCH.

It prints the modification date, relative to now, of the passed FILE.

=head1 DEPENDENCIES

On Ubuntu, this requires the following packages:

  $ apt-get install libmodern-perl-perl libtime-duration-perl

On CentOS, this requires the following packages:

  $ yum install perl-Modern-Perl perl-Time-Duration perl-Time-Piece

=cut
